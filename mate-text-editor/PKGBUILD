# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-text-editor
pkgver=1.6.0
pkgrel=3
pkgdesc="A powerful text editor for MATE"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('enchant' 'glib2' 'gtk2' 'gtksourceview2' 'iso-codes' 'libsm' 'libxml2'
         'python2' 'python2-gobject2' 'pygtksourceview2')
makedepends=('gtk-update-icon-cache' 'mate-common' 'mate-doc-utils')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org"
groups=('mate-extras')
install=mate-text-editor.install
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('52339cb8d265d7a538c9b3b3e6a37f68ece5100d18c029dfc87c3f4b61cf8288')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=python2 ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --enable-python \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    # Remove Python byte-code
    rm -f ${pkgdir}/usr/lib/pluma/plugins/*/*.py{c,o}
}
