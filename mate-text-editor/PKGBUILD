# Maintainer: neocrust
pkgname=mate-text-editor
pkgver=1.1.0
pkgrel=1
pkgdesc="An powerfull text editor for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-conf' 'gtk2' 'gtksourceview2' 'libxml2' 'pygtksourceview2')
makedepends=('pkgconfig' 'mate-doc-utils' 'intltool')
options=('!emptydirs' '!libtool')
conflicts=('pluma')
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
install=mate-text-editor.install
source=(./mate-text-editor-1.1.0.tar.gz)
sha256sums=('a06bd66d398be030f4eb8e7ee9130c51749a5488d83ed993720f1ce251748de5')

build() {
    cd "${srcdir}/${pkgname}"

    PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --libexecdir=/usr/lib/ \
        --disable-scrollkeeper --enable-python || return 1
    make || return 1

}

package() {
    cd "${srcdir}/${pkgname}"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}


