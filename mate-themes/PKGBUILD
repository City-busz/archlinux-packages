# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-themes
pkgver=1.6.2
pkgrel=1
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('gtk2' 'gtk-engines' 'gtk-engine-murrine' 'librsvg' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate')
replaces=('gnome-themes')
conflicts=('gnome-themes')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('271653845a658c10279dc3ad79f9370f539a299a')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-all-themes \
        --enable-test-themes \
        --disable-placeholders
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
