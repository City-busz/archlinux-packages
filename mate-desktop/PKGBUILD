# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=mate-desktop
pkgver=0
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=(GPL LGPL)
depends=('dconf' 'gtk3' 'python2' 'startup-notification')
makedepends=('git' 'gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
    sed -i 's|\/usr\/bin\/python|\/usr\/bin\/env python2|' tools/mpaste
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --enable-mpaste \
        --disable-static \
        --disable-schemas-compile \
        --enable-gtk-doc
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
