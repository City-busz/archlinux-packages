# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-desktop
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")

pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=(GPL LGPL)
depends=('dconf' 'gtk2' 'hicolor-icon-theme' 'libunique' 'python2'
         'startup-notification' 'ttf-dejavu')
makedepends=('git' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('lightdm-gtk2-greeter: A lightweight display manager'
            'orca: Screen reader for blind or visually impaired individuals'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')
install=${_pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    # See the following for '--disable-desktop-docs' justification
    #  - https://github.com/mate-desktop/mate-desktop/issues/68
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=2.0 \
        --enable-mpaste \
        --disable-static \
        --disable-schemas-compile \
        --disable-desktop-docs \
        --enable-gtk-doc
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
