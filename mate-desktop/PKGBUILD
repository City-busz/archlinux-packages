# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=mate-desktop
pkgver=0
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=(GPL LGPL)
depends=('dconf' 'gtk3' 'libunique' 'python2' 'startup-notification' 'ttf-dejavu')
makedepends=('git' 'mate-common' 'yelp-tools')
optdepends=('lightdm-gtk3-greeter: A lightweight display manager'
            'onboard: On-screen keyboard useful for mobility impaired users'
            'orca: Screen reader for blind or visually impaired individuals'
            'yelp: for reading MATE help documents')
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(date +%Y%m%d).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
    sed -i 's|\/usr\/bin\/python|\/usr\/bin\/env python2|' tools/mpaste
}

build() {
    # See the following for '--disable-desktop-docs' justification
    #  - https://github.com/mate-desktop/mate-desktop/issues/68
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --enable-mpaste \
        --disable-static \
        --disable-schemas-compile \
        --disable-desktop-docs \
        --enable-gtk-doc
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
