# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-netbook
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="A simple window management tool (GTK3 version [EXPERIMENTAL])."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libfakekey' 'libwnck3' 'libunique3' 'mate-panel>=1.9.4')
makedepends=('git' 'mate-common')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
provides=("${pkgbase}")
replaces=("${pkgbase}")
conflicts=("${pkgbase}")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-netbook-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
