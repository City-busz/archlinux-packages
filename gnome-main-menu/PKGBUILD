# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=gnome-main-menu
pkgver=0
pkgrel=1
pkgdesc="A MATE panel applet similar to the traditional menu but with a few additions"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.9.3' 'gtk2' 'libunique' 'mate-control-center>=1.9.0' 'mate-desktop>=1.9.3' 'mate-panel>=1.9.4' 'networkmanager')
makedepends=('git' 'mate-common')
source=("${pkgname}"::"git+https://git.gnome.org/browse/${pkgname}")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git rev-list --count HEAD)" "$(git log -1 --format=%cd --date=short | tr -d -)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-caja-extension \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
