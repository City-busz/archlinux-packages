# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.1
pkgname=mate-menus
pkgver=0
pkgrel=1
pkgdesc="MATE menu specifications"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'python2')
makedepends=('git' 'gobject-introspection' 'mate-common')
groups=('mate-unstable' 'mate-unstable-gtk3')
source=("${pkgname}"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
