# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-menus
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")

pkgdesc="MATE menu specifications"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'python2')
makedepends=('git' 'gobject-introspection' 'mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
