# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.1
pkgbase=mate-user-guide
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="MATE User Guide"
groups=('mate-unstable')
url="http://mate-desktop.org"
arch=('any')
license=('GPL' 'LGPL')
depends=('yelp')
makedepends=('git' 'mate-common' 'yelp-tools')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"

    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --enable-gtk-doc
    make
}

package_mate-user-guide() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
