# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=caja-extensions
pkgname=(caja-gksu-gtk3 caja-image-converter-gtk3 caja-open-terminal-gtk3 caja-sendto-gtk3 caja-share-gtk3)
pkgver=0
pkgrel=1
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja-extensions-common' 'caja-gtk3>=1.9.3' 'dbus-glib' 'gksu' 'gtk3' 'gupnp'
         'imagemagick' 'samba')
makedepends=('git' 'mate-common')
source=("$pkgbase"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=3.0
    make
}

package_caja-gksu-gtk3() {
    pkgdesc="Privilege granting extension for Caja using gksu (GTK3 version [EXPERIMENTAL])"
    depends=('caja-gtk3' 'caja-extensions-common' 'gksu')
    groups=('mate-unstable-extra-gtk3')
    conflicts=('caja-gksu')

    cd "${srcdir}/${pkgbase}"
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter-gtk3() {
    pkgdesc="A Caja extension for simple image conversions (GTK3 version [EXPERIMENTAL])"
    depends=('caja-gtk3' 'caja-extensions-common' 'imagemagick')
    groups=('mate-unstable-extra-gtk3')
    conflicts=('caja-image-converter')

    cd "${srcdir}/${pkgbase}"
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal-gtk3() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths (GTK3 version [EXPERIMENTAL])"
    depends=('caja-gtk3' 'caja-extensions-common')
    groups=('mate-unstable-extra-gtk3')
    conflicts=('caja-open-terminal')
    install=caja-open-terminal.install

    cd "${srcdir}/${pkgbase}"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto-gtk3() {
    pkgdesc="A Caja extension for sending files (GTK3 version [EXPERIMENTAL])"
    depends=('caja-gtk3' 'caja-extensions-common' 'dbus-glib' 'gupnp')
    groups=('mate-unstable-extra-gtk3')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('caja-sendto')
    install=caja-sendto.install

    cd "${srcdir}/${pkgbase}"
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share-gtk3() {
    pkgdesc="A Caja extension to quickly share a folder (GTK3 version [EXPERIMENTAL])"
    depends=('caja-gtk3' 'caja-extensions-common' 'samba')
    groups=('mate-unstable-extra-gtk3')
    conflicts=('caja-share')
    
    cd "${srcdir}/${pkgbase}"
    make -C share DESTDIR="${pkgdir}" install
}
