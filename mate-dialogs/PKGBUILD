#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-dialogs
pkgver=1.5.0
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
arch=(i686 x86_64)
license=('LGPL')
depends=('libmatenotify')
makedepends=('intltool' 'gtk-doc' 'mate-doc-utils')
url="http://mate-desktop.org"
groups=('mate')
provides=('zenity')
conflicts=('zenity')
options=('!emptydirs' '!libtool')
source=(http://pub.mate-desktop.org/releases/1.5/$pkgname-$pkgver.tar.xz)
sha256sums=('507345805ee114d68ef37fcf78f00993db68c31f02d46d7eaf08abaff2eafb1d')


build() {
    cd "${srcdir}/$pkgname-$pkgver"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-scrollkeeper || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="${pkgdir}" install
    # hax!
    ln -s matedialog ${pkgdir}/usr/bin/zenity
}
