# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=mate-settings-daemon
_ver=1.9.80
pkgname=("${pkgbase}-pulseaudio-git" "${pkgbase}-gstreamer-git")
pkgver=0
pkgrel=1

url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'gstreamer0.10-base' 'gtk2'
         'libcanberra-pulse' 'libmatekbd' 'libnotify' 'libxt' 'mate-desktop'
         'nss' 'polkit' 'pulseaudio-alsa')
makedepends=('git' 'mate-common' 'perl-xml-parser')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"
    cp -a ${pkgbase}{-pulseaudio-git,-gstreamer-git}
    
    # build for pulseaudio 
    cd "${srcdir}/${pkgbase}-pulseaudio-git"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --enable-polkit \
        --enable-pulse \
        --disable-gstreamer \
        --disable-static
    make
    
    # build for gstreamer
    cd "${srcdir}/${pkgbase}-gstreamer-git"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --enable-polkit \
        --enable-gstreamer \
        --disable-pulse \
        --disable-static
    make
}

package_mate-settings-daemon-pulseaudio-git() {
    groups=('mate-unstable')
    pkgdesc="The MATE Settings daemon (pulseaudio)"
    depends=('dbus-glib' 'dconf' 'fontconfig' 'gtk2' 'libcanberra-pulse'
            'libmatekbd' 'libnotify' 'libxt' 'mate-desktop' 'nss' 'polkit'
            'pulseaudio-alsa')
    conflicts=("${pkgbase}-gstreamer-git" 'mate-media-gstreamer-git')
    provides=("${pkgbase}")
    replaces=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-pulseaudio-git"
    make DESTDIR="${pkgdir}" install
}

package_mate-settings-daemon-gstreamer-git() {
    pkgdesc="The MATE Settings daemon (GStreamer)"
    depends=('dbus-glib' 'dconf' 'fontconfig' 'gstreamer0.10-base' 'gtk2'
             'libmatekbd' 'libnotify' 'libxt' 'mate-desktop' 'nss' 'polkit')
    conflicts=("${pkgbase}-pulseaudio-git" 'mate-media-pulseaudio-git')
    provides=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-gstreamer-git"
    make DESTDIR="${pkgdir}" install
}
