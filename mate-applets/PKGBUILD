# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.6.1
pkgrel=2
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'dbus' 'glib2' 'gsettings-desktop-schemas' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gtk2' 'gvfs' 'libgtop' 'libmateweather'
         'libmatewnck' 'libnotify' 'liboobs' 'libsoup' 'libxklavier' 'libxml2'
         'libxt' 'libx11' 'mate-desktop' 'mate-icon-theme' 'mate-panel'
         'mate-settings-daemon' 'polkit' 'pygtk' 'python2' 'python2-dbus'
         'python2-gobject2' 'python2-notify' 'sharutils' 'system-tools-backends'
         'upower' 'xproto')
makedepends=('docbook2x' 'docbook-xsl' 'gtk-update-icon-cache' 'mate-common'
             'mate-doc-utils' 'networkmanager')
groups=('mate-extras')
options=(!emptydirs)
replaces=('gnome-applets')
conflicts=('gnome-applets')
url="http://mate-desktop.org"
install=mate-applets.install
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8693cd88c4731796c09294c962d0ff12092d536121a5c41ddb06fffbc78aa00a')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-timer-applet \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
	make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -rf ${pkgdir}/usr/lib/python2.*/site-packages/mate_invest/*.py{c,o}
    rm -rf ${pkgdir}/usr/lib/python2.*/site-packages/timerapplet/*.py{c,o}
}
