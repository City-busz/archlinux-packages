# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.6.1
pkgrel=7
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://mate-desktop.org"
license=('GPL')
depends=('avahi' 'dbus' 'gsettings-desktop-schemas' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gtk2' 'gvfs' 'libgtop' 'libmateweather'
         'libmatewnck' 'libnotify' 'liboobs' 'libsoup' 'libxklavier' 'libxt'
         'mate-desktop' 'mate-icon-theme' 'mate-panel' 'mate-settings-daemon'
         'polkit' 'pygtk' 'python2' 'python2-dbus' 'python2-gobject2'
         'python2-notify' 'sharutils' 'system-tools-backends' 'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'mate-doc-utils' 'networkmanager' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
replaces=('gnome-applets')
conflicts=('gnome-applets')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz
        f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff)
sha1sums=('520b064b45c9e0677ea5dea0f51698d3bc982371'
          'bfd4136ef550af410b6ca4c7deebc7099d8addab')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    
    # battstat applet: ready for upower-1.0
    # https://github.com/mate-desktop/mate-applets/pull/63
    patch -Np1 -i ${srcdir}/f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff
    
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # Use python2
    sed -i 's/env python/env python2/' ${pkgdir}/usr/lib/${pkgname}/invest-applet

}
