# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-applets
pkgver=1.6.1
pkgrel=10
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://mate-desktop.org"
license=('GPL')
depends=('mate-character-map' 'mate-panel' 'pytnon2-dbus' 'python2-gobject'
         'upower')
depends=('avahi' 'dbus' 'gsettings-desktop-schemas' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gtk2' 'gvfs' 'libgtop' 'libmateweather'
         'libmatewnck' 'libnotify' 'liboobs' 'libsoup' 'libxklavier' 'libxt'
         'mate-desktop' 'mate-icon-theme' 'mate-panel' 'mate-settings-daemon'
         'polkit' 'pygtk' 'python2' 'python2-dbus' 'python2-gobject2'
         'python2-notify' 'sharutils' 'system-tools-backends' 'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'mate-doc-utils' 'networkmanager' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extras')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff
        1e464e89fa0305266e72e3fce43f49e59b4a8ef8.diff)
sha1sums=('520b064b45c9e0677ea5dea0f51698d3bc982371'
          '2ec083203e9d7609aa0e5eeb870160f00706c5e2'
          '36c4507e54b21fbcff49732077912dbc5283807a')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # battstat applet: ready for upower-1.0
    # - https://github.com/mate-desktop/mate-applets/pull/63
    patch -Np1 -i "${srcdir}/f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff"
    # - https://github.com/mate-desktop/mate-applets/pull/64
    patch -Np1 -i "${srcdir}/1e464e89fa0305266e72e3fce43f49e59b4a8ef8.diff"
    # Use python2
    sed -i 's/env python/env python2/' invest-applet/invest/{chart.py,invest-applet.py,mate-invest-chart}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
