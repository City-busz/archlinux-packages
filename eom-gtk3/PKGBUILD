# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=eom
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gobject-introspection' 'gtk3' 'exempi'
         'lcms2' 'libexif' 'libjpeg-turbo' 'librsvg' 'mate-desktop-gtk3>=1.9.3'
         'mate-icon-theme' 'startup-notification' 'zlib')
makedepends=('git' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra-gtk3')
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --with-librsvg \
        --disable-python
    make
}

package() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
