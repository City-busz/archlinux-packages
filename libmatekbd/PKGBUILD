pkgname=libmatekbd
pkgver=1.1.0
pkgrel=1
pkgdesc="MATE keyboard library"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libxklavier' 'mate-conf') #  'dconf'
makedepends=('intltool' 'pkgconfig')
groups=('mate')
url="http://matsusoft.com.ar/projects/mate/"
options=('!emptydirs' '!libtool')
install=libmatekbd.install
source=(./libmatekbd-1.1.0.tar.gz)
sha256sums=('e797fe70a2606a262e0ef41f7efdcc3b9265f11b94ea8ee6c8f305271239f15c')

build() {
    cd "${srcdir}/$pkgname"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-static || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname"
    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
