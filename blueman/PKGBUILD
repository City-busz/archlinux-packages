# Maintainer: Martin Wimpress <code@flexion.org>

_ver=1.2.3
pkgname=blueman
pkgver=0
pkgrel=1
pkgdesc="A GTK+ Bluetooth Manager"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/blueman-project/blueman"
depends=('bluez' 'dbus-glib' 'gconf' 'gtk3' 'mate-notification-daemon'
         'libnotify' 'polkit' 'pulseaudio' 'python2-dbus' 'python2-gobject2'
         'python2-pybluez' 'startup-notification')
optdepends=('dnsmasq: DHCP server for Network Access Point')
makedepends=('git' 'intltool' 'libtool' 'pyrex')
groups=('mate-unstable-extra')
source=("${pkgname}"::"git+https://github.com/blueman-project/${pkgname}.git#branch=bluez5"
        "use-python2.diff")
sha1sums=('SKIP'
          '73bcad1a5e42474573cc565e3a250f560ca78e8d')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(date +%Y%m%d).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    # Coerce the build system to use python2
    patch -Np1 -i "${srcdir}/use-python2.diff"
    sed -i 's|AM_CHECK_PYTHON_HEADERS|#AM_CHECK_PATH_HEADERS|' configure.ac
    sed -i 's|@PYTHON_INCLUDES@|-I\/usr\/include\/python2.7|' module/Makefile.am
    sed -i 's|@PYTHON_INCLUDES@|-I\/usr\/include\/python2.7|' sendto/Makefile.am

    # Replace the python interpreter with python2
    for PY in apps/blueman-*
    do
        sed -i 's/python/python2/' "${PY}"
    done
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgnme} \
        --with-gtk=3.0 \
        --disable-hal \
        --disable-sendto \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
