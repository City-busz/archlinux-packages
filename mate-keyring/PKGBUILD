#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-keyring
pkgver=1.2.0
pkgrel=1
pkgdesc="MATE Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('dbus')
makedepends=('intltool' 'pkgconfig' 'python2')
url="http://matsusoft.com.ar/projects/mate/"
groups=('mate')
options=('!libtool' '!emptydirs')
install=mate-keyring.install
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('4275cf7e0021581458e4e41dcf6c7c2fd41d0828dbd818e624d79f33c202f4a5')


build() {
    cd "${srcdir}/$pkgname-$pkgver"

    ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --libexecdir=/usr/lib/mate-keyring \
        --with-pam-dir=/lib/security --with-root-certs=/etc/ssl/certs || return 1

    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
