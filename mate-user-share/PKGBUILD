#Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.6.0
pkgrel=2
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('apache' 'dbus' 'glib2' 'gtk2' 'libcanberra' 'libnotify' 'libunique'
         'mate-bluetooth' 'mate-file-manager' 'mod_dnssd' 'obex-data-server')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
groups=('mate-extras')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org"
install=mate-user-share.install
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
md5sums=('ee26e90c51aa0d1f04d66cb2daf50fca')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=python2 ./autogen.sh \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper
	make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make  DESTDIR="${pkgdir}" install
    rm -f ${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf
}
