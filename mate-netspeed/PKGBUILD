# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.8.99
pkgname=mate-netspeed
pkgver=0
pkgrel=1
pkgdesc="A MATE applet that shows how much traffic occurs on a specified network device."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libgtop' 'mate-panel' 'wireless_tools')
makedepends=('git' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra')
source=("$pkgname"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
