# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-system-tools
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")

pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'liboobs' 'mate-polkit' 'system-tools-backends' 'wireless_tools')
makedepends=('git' 'caja' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('ntp: Automatic time synchronisation.'
            'caja: provides Caja Shares extension'
            'yelp: for reading MATE help documents')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')
install=${_pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/lib/caja/extensions-2.0/libcaja-gst-shares.a"
}
