# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=mate-backgrounds
pkgver=0
pkgrel=1
pkgdesc="Background images and data for MATE"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
makedepends=('git' 'mate-common' )
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
