# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-backgrounds
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")

pkgdesc="Background images and data for MATE"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
makedepends=('git' 'mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-unstable')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
