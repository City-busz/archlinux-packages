# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-netspeed
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="A MATE applet that shows how much traffic occurs on a specified network device. (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libgtop' 'mate-panel-gtk3>=1.9.4' 'wireless_tools')
makedepends=('git' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra-gtk3')
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-netspeed-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
