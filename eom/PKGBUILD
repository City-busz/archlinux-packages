# Contributor: Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'exempi' 'lcms' 'libexif'
         'libjpeg-turbo' 'mate-desktop' 'mate-icon-theme' 'pygtk' 'python2'
         'python2-gobject2' 'startup-notification' 'yelp' 'zlib')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs' '!libtool')
groups=('mate')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git"
        '01-fix.diff')
sha1sums=('SKIP'
          '8a976cfd7db19c7362df1d27a76e9d20472da733')
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    # This patch can be remove when https://github.com/mate-desktop/eom/pull/31
    # is merged.
    cd ${srcdir}/${pkgname}
    patch -Np1 -i ${srcdir}/01-fix.diff
}

build() {
    cd ${srcdir}/${pkgname}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --localstatedir=/var
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR=${pkgdir} install
}
