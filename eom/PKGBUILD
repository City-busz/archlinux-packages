# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=eom
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'exempi' 'lcms2' 'libexif'
         'libjpeg-turbo' 'librsvg' 'mate-desktop>=1.9.3' 'mate-icon-theme'
         'pygtk' 'python2-gobject2' 'startup-notification' 'zlib')
makedepends=('git' 'gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra')
provides=("${pkgbase}")
replaces=("${pkgbase}-gtk3")
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --with-librsvg \
        --enable-python
    make
}

package_eom() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
