# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=caja-extensions
pkgname=(caja-extensions-common caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share)
pkgver=0
pkgrel=1
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.9.3' 'dbus-glib' 'gksu' 'gtk2' 'gupnp' 'imagemagick' 'samba')
makedepends=('git' 'mate-common')
source=("$pkgbase"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=2.0
    make
}

package_caja-extensions-common() {
    pkgdesc="Caja extensions (common files)"
    depends=()

    cd "${srcdir}/${pkgbase}"
    make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
    pkgdesc="Privilege granting extension for Caja using gksu (GTK2 version)"
    depends=('caja' 'caja-extensions-common' 'gksu')
    groups=('mate-unstable-extra')
    conflicts=('caja-gksu-gtk3')

    cd "${srcdir}/${pkgbase}"
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
    pkgdesc="A Caja extension for simple image conversions (GTK2 version)"
    depends=('caja' 'caja-extensions-common' 'imagemagick')
    groups=('mate-unstable-extra')
    conflicts=('caja-image-converter-gtk3')

    cd "${srcdir}/${pkgbase}"
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths (GTK2 version)"
    depends=('caja' 'caja-extensions-common')
    groups=('mate-unstable-extra')
    conflicts=('caja-open-terminal-gtk3')
    install=caja-open-terminal.install

    cd "${srcdir}/${pkgbase}"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
    pkgdesc="A Caja extension for sending files (GTK2 version)"
    depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
    groups=('mate-unstable-extra')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('caja-sendto-gtk3')
    install=caja-sendto.install

    cd "${srcdir}/${pkgbase}"
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
    pkgdesc="A Caja extension to quickly share a folder (GTK2 version)"
    depends=('caja' 'caja-extensions-common' 'samba')
    groups=('mate-unstable-extra')
    conflicts=('caja-share-gtk3')
    
    cd "${srcdir}/${pkgbase}"
    make -C share DESTDIR="${pkgdir}" install
}
