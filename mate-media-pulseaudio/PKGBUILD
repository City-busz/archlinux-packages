# Contributor: Martin Wimpress <code@flexion.org>

_pkgname=mate-media
pkgname=${_pkgname}-pulseaudio
pkgver=1.7.0
pkgrel=1
pkgdesc="MATE Media Tools (pulseaudio)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'gstreamer0.10-base-plugins' 'libnotify'
         'mate-panel' 'mate-settings-daemon-pulseaudio' 'pulseaudio')
makedepends=('mate-settings-daemon' 'mate-icon-theme' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs' '!libtool')
groups=('mate')
conflicts=("${_pkgname}-gstreamer" 'mate-settings-daemon-gstreamer')
provides=("${_pkgname}")
replaces=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('32730c0c5ca20d41f11390a154875a1220d9ba9c')
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${_pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${_pkgname}
	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/${_pkgname} \
		--localstatedir=/var \
		--enable-pulseaudio \
		--disable-gstmix \
		--disable-gst-mixer-applet \
		--disable-static \
		--disable-scrollkeeper
	make    
}

package() {
    cd ${srcdir}/${_pkgname}
    make DESTDIR=${pkgdir} install
}
