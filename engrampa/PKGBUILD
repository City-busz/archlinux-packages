# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=engrampa
pkgver=1.6.9.85.0e4df16
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('bzip2' 'caja' 'desktop-file-utils' 'gtk2' 'gzip' 'tar' 'yelp')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('unrar: for RAR uncompression'
'zip: for ZIP archives' 'unzip: for ZIP archives'
'p7zip: 7zip compression utility' 'arj: for ARJ archives'
'unace: extraction tool for the proprietary ace archive format')
options=('!libtool' '!emptydirs')
groups=('mate-extras')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${pkgname}
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper \
        --disable-packagekit
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR=${pkgdir} install
}
