# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=atril
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")

pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk2' 'libsm' 'libspectre'
         'mate-desktop' 'mate-icon-theme' 'poppler-glib' 'zlib')
makedepends=('git' 'caja' 'djvulibre' 'gobject-introspection' 'libgxps' 'mate-common'
             'perl-xml-parser' 'texlive-bin' 'yelp-tools')
optdepends=('caja: Document Properties extension'
            'djvulibre: DjVu support'
            'libgxps: XPS support'
            'p7zip: cbz and cb7 compressed comic books'
            'texlive-bin: DVI support'
            'unrar: cbr compressed comic books'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-unstable-extra')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')
install=${_pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
