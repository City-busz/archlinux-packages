# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.3
pkgbase=marco
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="A window manager for MATE  (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt' 'mate-desktop-gtk3>=1.9.3'
         'startup-notification' 'zenity')
makedepends=('git' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
groups=('mate-unstable-gtk3')
provides=("${pkgbase}")
replaces=("${pkgbase}")
conflicts=("${pkgbase}")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package_marco-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
