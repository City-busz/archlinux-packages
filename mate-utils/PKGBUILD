# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-utils
pkgver=1.6.0
pkgrel=8
pkgdesc="Common MATE utilities for viewing disk usage, logs and fonts, taking screenshots, managing dictionaries and searching files"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'libsm' 'mate-desktop' 'mate-panel'
         'zlib')
makedepends=('mate-common' 'mate-doc-utils' 'mate-panel' 'perl-xml-parser'
             'rarian')
optdepends=('mate-panel: Look up words in dictionaries from the panel')
options=('!emptydirs')
groups=('mate-extras')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        'patch.diff'
        '422504e251442ca9921c4218151cffb6548280e6.diff')
sha1sums=('a43b45cccffd6116c49475d7511cf58b219c0d72'
          'f1eb654b90423ab5468edd57dc33f3f7f9c3f390'
          '71d8115a2044823936289c39bbaf32070d343f76')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Fix save directory selection
    patch -Np1 -i "${srcdir}/patch.diff"
    # Fix masking for area or window screenshots
    # https://github.com/mate-desktop/mate-utils/pull/45
    patch -Np1 -i "${srcdir}/422504e251442ca9921c4218151cffb6548280e6.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
