#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=libmateweather
pkgver=1.2.0
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('mate-conf') #  'libsoup-mate'  'mate-icon-theme'
makedepends=('pkgconfig' 'intltool' 'gtk-doc')
groups=('mate')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org"
install=libmateweather.install
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('440bca0ffaf762b235c82a3beec5c80579e6239cbd4addda0c5ec3c3858c66c6')


build() {
    cd "${srcdir}/$pkgname-$pkgver"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-locations-compression || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain libmateweather ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
