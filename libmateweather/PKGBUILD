# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.1
pkgbase=libmateweather
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'libsoup')
makedepends=('git' 'mate-common')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"
    mv ${pkgbase} "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"

    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make
}

package_libmateweather() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'libsoup')
    
    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_libmateweather-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    conflicts=("${pkgbase}")
    depends=('dconf' 'gtk3' 'libsoup')
    
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
