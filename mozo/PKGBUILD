# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mozo
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="MATE menu editing tool (GTK2 version)"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('mate-menus>=1.9.1' 'pygtk' 'python2-gobject')
makedepends=('git' 'mate-common')
groups=('mate-unstable-extra')
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr
    make
}

package_mozo() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
