# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=mozo
pkgver=0
pkgrel=1
pkgdesc="MATE menu editing tool"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('mate-menus' 'pygtk' 'python2-gobject2')
makedepends=('git' 'mate-common'  'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra')
source=("$pkgname"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
