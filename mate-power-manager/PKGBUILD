#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>
#Contributor: hekel <hekel@archlinux.info>

pkgname=mate-power-manager
pkgver=1.6.2
pkgrel=2
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'dbus' 'glib2' 'gtk2' 'libcanberra' 'libgcrypt' 'libmatekeyring'
         'libnotify' 'libunique' 'libx11' 'libxext' 'libxrandr'
         'mate-notification-daemon' 'mate-panel' 'upower')
makedepends=('docbook-xml' 'mate-common' 'mate-doc-utils')
# docbook2x xmlto should be a makedepends but the build fails.
options=('!emptydirs')
install=mate-power-manager.install
url="http://mate-desktop.org"
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('b80d531271f9a7ee692d69173b28f46feba025b4ffcc9e0ab2f5b09934f8d80f')

build() {

	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-unique \
		--disable-scrollkeeper

	make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
