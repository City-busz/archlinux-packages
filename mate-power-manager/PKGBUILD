# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: hekel <hekel@archlinux.info>

pkgname=mate-power-manager
pkgver=1.6.2
pkgrel=6
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'dbus' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libgcrypt'
         'libmatekeyring' 'libnotify' 'libunique' 'mate-notification-daemon' 'mate-panel' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'mate-doc-utils' 'perl-xml-parser' 'xmlto')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        63ff37f7d142800ceae6b429513996206ad3f14d.diff
        8cc7c8067a15188fcb39ef9ab02ae5271ee9c1d3.diff
        c7d5cb7d295716343e257a419f2479fcc32e6eff.diff
        3908c55155652719c630537c73911590e1f42cc9.diff)
sha1sums=('fb8f28a32620e3e288a887edcad1059c7ff84fd4'
          '604163a69691504bfe651a46177b94b91d06e693'
          '1a16f5e00260c1ecce9ceff16337558e23c6af62'
          '6ffde1f11bdde5ec28fc1e5b63ebe64d57df10e9'
          'dbc3d679e7fa3fa99d4ccf9be375511d52e5021d')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    # fix dbus path gpm phone manager
    # https://github.com/mate-desktop/mate-power-manager/commit/63ff37f7d142800ceae6b429513996206ad3f14d
    patch -Np1 -i "${srcdir}/63ff37f7d142800ceae6b429513996206ad3f14d.diff"
    
    # fix systemd-login1 support for gpm_manager_blank_screen
    # https://github.com/mate-desktop/mate-power-manager/commit/8cc7c8067a15188fcb39ef9ab02ae5271ee9c1d3
    patch -Np1 -i "${srcdir}/8cc7c8067a15188fcb39ef9ab02ae5271ee9c1d3.diff"
    
    # fix comment in gpm-manager.c
    # https://github.com/mate-desktop/mate-power-manager/commit/c7d5cb7d295716343e257a419f2479fcc32e6eff
    patch -Np1 -i "${srcdir}/c7d5cb7d295716343e257a419f2479fcc32e6eff.diff"
    
    # fix typo in gpm_control_suspend, gpm-control.c
    # this fix 'suspend on lid close'
    # https://bugzilla.redhat.com/show_bug.cgi?id=1012718
    # https://github.com/mate-desktop/mate-power-manager/commit/3908c55155652719c630537c73911590e1f42cc9
    patch -Np1 -i "${srcdir}/3908c55155652719c630537c73911590e1f42cc9.diff"
    
    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
