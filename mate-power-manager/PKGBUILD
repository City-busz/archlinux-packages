#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>
#Contributor: hekel <hekel@archlinux.info>

pkgname=mate-power-manager
pkgver=1.6.1
pkgrel=3
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('libnotify' 'libunique' 'libcanberra' 'hicolor-icon-theme' 'upower' 'libmatekeyring')
makedepends=('intltool' 'pkgconfig' 'mate-doc-utils' 'mate-panel' 'mate-control-center')
options=('!emptydirs')
install=mate-power-manager.install
url="http://mate-desktop.org"
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('f402c110c5d2fb98fb4f550bde4274cdb40300773b9b3772a507c07fc710af47')

build() {

	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-scrollkeeper 
		#--enable-applets
		#--with-dbus-services=/usr/share/dbus-1/services \
		#--with-dpms-ext \

	make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
