# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=mate-media
_ver=1.9.80
pkgname=("${pkgbase}-pulseaudio-git" "${pkgbase}-gstreamer-git")
pkgver=0
pkgrel=1

url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra-pulse' 'libunique'
         'marco' 'mate-desktop' 'pulseaudio')
makedepends=('git' 'mate-common' 'mate-panel' 'mate-settings-daemon-gstreamer'
             'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"
    cp -a ${pkgbase}{,-gstreamer}

    # build for pulseaudio
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-pulseaudio \
        --disable-gstmix \
        --disable-gst-mixer-applet \
        --disable-static
    make

    # build for gstreamer
    cd "${srcdir}/${pkgbase}-gstreamer"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-gstmix \
        --enable-gst-mixer-applet \
        --enable-deprecated \
        --disable-pulseaudio \
        --disable-static
    make
}

package_mate-media-pulseaudio-git() {
    groups=('mate-unstable-extra')
    pkgdesc="MATE Media Tools (pulseaudio)"
    depends=('libcanberra-pulse' 'libunique' 'marco' 'mate-desktop' 'pulseaudio')
    conflicts=("${pkgbase}-gstreamer-git" 'mate-settings-daemon-gstreamer-git')
    provides=("${pkgbase}")
    replaces=("${pkgbase}")
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}

package_mate-media-gstreamer-git() {
    pkgdesc="MATE Media Tools (GStreamer)"
    depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra' 'marco' 
             'mate-desktop')
    optdepends=('mate-panel: Volume control for the panel')
    conflicts=("${pkgbase}-pulseaudio-git" 'mate-settings-daemon-pulseaudio-git')
    provides=("${pkgbase}")
    install=${pkgbase}-gstreamer.install
    cd "${srcdir}/${pkgbase}-gstreamer"
    make DESTDIR="${pkgdir}" install
}
