# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgname=mate-media
pkgver=0
pkgrel=1
url="http://mate-desktop.org"
groups=('mate-unstable-extra')
pkgdesc="MATE Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra-pulse' 'libmatemixer' 'libunique3' 'marco'
         'mate-desktop')
makedepends=('git' 'mate-common' 'mate-panel' 'mate-settings-daemon'
             'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
conflicts=("${pkgname}-gstreamer-git" "${pkgbase}-pulseaudio-git")
provides=("${pkgname}")
replaces=("${pkgname}-gstreamer-git" "${pkgbase}-pulseaudio-git")
source=("${pkgname}"::"git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
