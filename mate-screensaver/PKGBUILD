# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.1
pkgbase=mate-screensaver
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc='Screensaver for MATE (GTK2 version)'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd>=1.9.1' 'libnotify' 'libxss' 'mate-desktop>=1.9.3'
         'mate-menus>=1.9.1' 'mate-session-manager>=1.9.3')
makedepends=('git' 'mate-common' 'xmlto')
optdepends=('mate-power-manager: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.')
groups=('mate-unstable-extra')
provides=("${pkgbase}")
replaces=("${pkgbase}-gtk3")
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --with-gtk=2.0 \
        --disable-static
    make
}

package_mate-screensaver() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
