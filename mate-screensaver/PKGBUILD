# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-screensaver
_ver=1.9.80
pkgname=${_pkgname}-git
pkgver=0
pkgrel=1
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")

pkgdesc='Screensaver for MATE'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd' 'libnotify' 'libxss' 'mate-desktop'
         'mate-menus' 'mate-session-manager')
makedepends=('git' 'mate-common' 'perl-xml-parser' 'xmlto' 'yelp-tools')
optdepends=('mate-power-manager: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-unstable-extra')
source=("$pkgname"::"git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')
install=${_pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo ${_ver}.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
