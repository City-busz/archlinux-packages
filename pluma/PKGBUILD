# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=pluma
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="A powerful text editor for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'enchant' 'gtk2' 'gtksourceview2' 'iso-codes'
         'libsm' 'mate-desktop>=1.9.3' 'pygtksourceview2' 'python2-gobject' 'zenity')
makedepends=('git' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra')
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
    sed -i 's/python/python2/' plugins/externaltools/data/switch-c.tool.in
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=2.0 \
        --enable-gtk-doc \
        --enable-python
    make
}

package_pluma() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
