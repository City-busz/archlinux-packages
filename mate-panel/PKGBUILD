#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-panel
pkgver=1.2.0
pkgrel=1
pkgdesc="The MATE Panel"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libwnck' 'mate-conf' 'mate-menus' 'mate-desktop' 'librsvg' 'hicolor-icon-theme' 'libcanberra' 'libmatecomponentui' 'libmateweather') # 'evolution-data-server'
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool' 'networkmanager')
options=('!emptydirs' '!libtool')
install=mate-panel.install
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('67a34e087c2af28b3e626a3389996007f2d62a920f19c6bf6dc737fcf3d5f35b')

url="http://mate-desktop.org"
groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/mate-panel \
        --disable-scrollkeeper \
        --enable-introspection \
        --enable-matecomponent || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
