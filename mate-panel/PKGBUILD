# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-panel
pkgver=1.6.1
pkgrel=2
pkgdesc="The MATE Panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'cairo' 'dbus' 'dconf' 'gtk2' 'libmatewnck' 'mate-menus'
         'mate-desktop' 'mate-polkit' 'networkmanager' 'libcanberra' 'libice'
         'libmateweather' 'librsvg' 'libsm' 'libsoup' 'libx11' 'libxau' 'libxrandr'
         'pango' 'python2')
makedepends=('gobject-introspection' 'mate-common' 'mate-doc-utils')
options=('!emptydirs' '!libtool')
install=mate-panel.install
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('88bf0620e1eba7e2e165060ac6fa21896b8e24d1ac742ff6f8c884c5408dd53b')
url="http://mate-desktop.org"
groups=('mate')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
