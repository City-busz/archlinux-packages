# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.5
pkgbase=mate-settings-daemon
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="The MATE Settings daemon (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'gtk3' 'libcanberra-pulse'
         'libmatekbd-gtk3>=1.9.0' 'libmatemixer' 'libnotify' 'libxt' 'mate-desktop-gtk3>=1.9.3'
         'nss' 'polkit')
makedepends=('git' 'mate-common')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
groups=('mate-unstable-gtk3')
provides=("${pkgbase}")
replaces=("${pkgbase}")
conflicts=("${pkgbase}")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --enable-polkit \
        --disable-static
    make
}

package_mate-settings-daemon-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
