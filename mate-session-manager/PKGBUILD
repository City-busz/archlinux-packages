# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.6.1
pkgrel=4
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('dbus' 'desktop-file-utils' 'gobject-introspection' 'gtk2' 'libice'
         'libsm' 'libxau' 'libxt' 'libxtst' 'mate-file-manager' 'mate-panel'
         'mate-polkit' 'mate-settings-daemon' 'mate-window-manager'
         'startup-notification' 'systemd')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto')
options=('!emptydirs' '!libtool')
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz
        f0b5b1ab78879d64f80117415aacf029359afea5.diff
        491b2c13b8df02512c47c101fb5bc2e769ad7273.diff)
sha1sums=('46188f6c02cc5978688af9d5db4be09f1b9199a5'
          '20fb99c29731d05191cd0029d0573db784b06ccf'
          'c23e34609b50914515a31322ed3eb2dbd05e2dad')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # fix systemd-login1 support for hibernate/suspend
    # https://github.com/mate-desktop/mate-session-manager/commit/f0b5b1ab78879d64f80117415aacf029359afea5
    patch -p1 < ../../f0b5b1ab78879d64f80117415aacf029359afea5.diff

    # upower: make upower optional
    # https://github.com/mate-desktop/mate-session-manager/issues/37
    # https://github.com/mate-desktop/mate-session-manager/pull/38
    patch -p1 < ../../491b2c13b8df02512c47c101fb5bc2e769ad7273.diff

    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-upower \
        --disable-introspection
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
