#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-session-manager
pkgver=1.6.1
pkgrel=2
pkgdesc="The MATE Session Handler"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('dbus' 'glib2' 'gobject-introspection' 'gtk2' 'libice' 'libsm'
         'libx11' 'libxau' 'libxext' 'libxrender' 'libxt' 'libxtst'
         'mate-file-manager' 'mate-panel' 'mate-polkit' 'mate-settings-daemon'
         'mate-window-manager' 'startup-notification' 'systemd' 'upower')
makedepends=('mate-common' 'xmlto')
options=('!emptydirs' '!libtool')
install=mate-session-manager.install
url="http://mate-desktop.org"
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('4ee7b4ae18df5becd986b05634428edf0ad8de150a14dc29848b6d34ab312ce9')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --disable-introspection || return 1

    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="$pkgdir/" install
}
