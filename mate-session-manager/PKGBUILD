pkgname=mate-session-manager
pkgver=1.1.2
pkgrel=1
pkgdesc="The MATE Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('upower' 'gtk2' 'mate-conf' 'hicolor-icon-theme' 'libxtst' 'mate-polkit') #  'startup-notification'
makedepends=('pkgconfig' 'intltool' 'xtrans')
options=('!emptydirs' '!libtool')
install=mate-session-manager.install
url="http://matsusoft.com.ar/projects/mate/"
groups=('mate')
source=(./${pkgname}-${pkgver}.tar.gz)
sha256sums=('c058483ee0c0a33cc93db500b98dfe3727a186fe6ac13f733e0daaf70267ec60')


build() {
    cd "${srcdir}/$pkgname"

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
	    --libexecdir=/usr/lib || return 1

    make || return 1
}

package() {
    cd "${srcdir}/$pkgname"

    make DESTDIR="${pkgdir}" MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

    install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1

    # --disable-schemas-compile <- check if work <- nop
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
