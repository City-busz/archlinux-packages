# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-utils
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="Common MATE utilities for viewing disk usage, logs and fonts, taking screenshots, managing dictionaries and searching files (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'libcanberra' 'libgtop' 'libsm' 'zlib')
makedepends=('git' 'mate-common' 'mate-panel-gtk3' 'rarian' 'yelp-tools')
optdepends=('mate-panel-gtk3: Look up words in dictionaries from the panel'
            'yelp: for reading MATE help documents')
groups=('mate-unstable-extra-gtk3')
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=3.0 \
        --sysconfdir=/etc \
        --enable-deprecated \
        --disable-maintainer-flags \
        --disable-static
    make
}

package_mate-utils-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
